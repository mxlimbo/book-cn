<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=GB_2312-80">
<title>AppendChunk 和 GetChunk 方法范例</title>
<style>@import url(/stylesheets/msdn_ie4.css);</style>
<link disabled rel="stylesheet" href="/stylesheets/msdn_ie3.css"></HEAD>
<BODY>

<h3><a name="mdmthappendchunkx"></a>AppendChunk 和 GetChunk 方法范例</h3>
<p>
该范例使用 AppendChunk 和 GetChunk 方法用其他记录中的数据填写图像字段。</p>
<pre>Public Sub AppendChunkX()

   Dim cnn1 As ADODB.Connection
   Dim rstPubInfo As ADODB.Recordset
   Dim strCnn As String
   Dim strPubID As String
   Dim strPRInfo As String
   Dim lngOffset As Long
   Dim lngLogoSize As Long
   Dim varLogo As Variant
   Dim varChunk As Variant
   
   Const conChunkSize = 100

   ' 打开连接。
   Set cnn1 = New ADODB.Connection
      strCnn = "Provider=sqloledb;" &amp; _
      "Data Source=srv;Initial Catalog=pubs;User Id=sa;Password=; "
   cnn1.Open strCnn
   
   ' 打开 pub_info 表。
   Set rstPubInfo = New ADODB.Recordset
   rstPubInfo.CursorType = adOpenKeyset
   rstPubInfo.LockType = adLockOptimistic
   rstPubInfo.Open "pub_info", cnn1, , , adCmdTable
   
   ' 提示复制徽标。
   strMsg = "Available logos are : " &amp; vbCr &amp; vbCr
   Do While Not rstPubInfo.EOF
      strMsg = strMsg &amp; rstPubInfo!pub_id &amp; vbCr &amp; _
         Left(rstPubInfo!pr_info, InStr(rstPubInfo!pr_info, ",") - 1) &amp; _
         vbCr &amp; vbCr
      rstPubInfo.MoveNext
   Loop
   strMsg = strMsg &amp; "Enter the ID of a logo to copy:"
   strPubID = InputBox(strMsg)
   
   ' 将徽标大块复制到变量中。
   rstPubInfo.Filter = "pub_id = '" &amp; strPubID &amp; "'"
   lngLogoSize = rstPubInfo!logo.ActualSize
   Do While lngOffset &lt; lngLogoSize
      varChunk = rstPubInfo!logo.GetChunk(conChunkSize)
      varLogo = varLogo &amp; varChunk
      lngOffset = lngOffset + conChunkSize
   Loop
   
   ' 从用户处得到数据。
   strPubID = Trim(InputBox("Enter a new pub ID:"))
   strPRInfo = Trim(InputBox("Enter descriptive text:"))
   
   ' 添加新记录，大块复制徽标。
   rstPubInfo.AddNew
   rstPubInfo!pub_id = strPubID
   rstPubInfo!pr_info = strPRInfo

   lngOffset = 0 ' 重置位移。
   Do While lngOffset &lt; lngLogoSize
      varChunk = LeftB(RightB(varLogo, lngLogoSize - lngOffset), _
         conChunkSize)
      rstPubInfo!logo.AppendChunk varChunk
      lngOffset = lngOffset + conChunkSize
   Loop
   rstPubInfo.Update
   
    ' 显示新添加的数据。
   MsgBox "New record: " &amp; rstPubInfo!pub_id &amp; vbCr &amp; _
      "Description: " &amp; rstPubInfo!pr_info &amp; vbCr &amp; _
      "Logo size: " &amp; rstPubInfo!logo.ActualSize

   ' 删除新记录，因为这只是演示。
   rstPubInfo.Requery
   cnn1.Execute "DELETE FROM pub_info " &amp; _
      "WHERE pub_id = '" &amp; strPubID &amp; "'"

   rstPubInfo.Close
   cnn1.Close   

End Sub
</pre>
</BODY>
</HTML>
