<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=GB_2312-80">
<title>ActiveConnection、CommandText、CommandTimeout、CommandType、Size 和 Direction 属性范例</title>
<style>@import url(/stylesheets/msdn_ie4.css);</style>
<link disabled rel="stylesheet" href="/stylesheets/msdn_ie3.css"></HEAD>
<BODY>

<h3><a name="mdproactiveconx"></a>ActiveConnection、CommandText、CommandTimeout、CommandType、Size 和 Direction 属性范例</h3>
<p>
该范例使用 <b>ActiveConnection</b>、<b>CommandText</b>、<b>CommandTimeout</b>、<b>CommandType</b>、<b>Size</b> 和 <b>Direction</b> 属性执行存储过程。</p>
<pre>Public Sub ActiveConnectionX()

   Dim cnn1 As ADODB.Connection
   Dim cmdByRoyalty As ADODB.Command
   Dim prmByRoyalty As ADODB.Parameter
   Dim rstByRoyalty As ADODB.Recordset
   Dim rstAuthors As ADODB.Recordset
   Dim intRoyalty As Integer
   Dim strAuthorID As String
   Dim strCnn As String

   ' 定义存储过程的命令对象。
   Set cnn1 = New ADODB.Connection
   strCnn = "Provider=sqloledb;" &amp; _
      "Data Source=srv;Initial Catalog=pubs;User Id=sa;Password=; "
   cnn1.Open strCnn
   Set cmdByRoyalty = New ADODB.Command
   Set cmdByRoyalty.<span class=cfe><b>ActiveConnection</b></span> = cnn1
   cmdByRoyalty.<span class=cfe><b>CommandText</b></span> = "byroyalty"
   cmdByRoyalty.<span class=cfe><b>CommandType</b></span> = adCmdStoredProc
   cmdByRoyalty.<span class=cfe><b>CommandTimeout</b></span> = 15
      
   ' 定义存储过程的输入参数。
   intRoyalty = Trim(InputBox( _
      "Enter royalty:"))
   Set prmByRoyalty = New ADODB.Parameter
   prmByRoyalty.Type = adInteger
   prmByRoyalty.<span class=cfe><b>Size</b></span> = 3
   prmByRoyalty.<span class=cfe><b>Direction</b></span> = adParamInput
   prmByRoyalty.Value = intRoyalty
   cmdByRoyalty.Parameters.Append prmByRoyalty
  
   ' 通过执行该命令创建记录集。
   Set rstByRoyalty = cmdByRoyalty.Execute()
      
   ' 打开作者表以便显示作者姓名。
   Set rstAuthors = New ADODB.Recordset
   rstAuthors.Open "authors", strCnn, , , adCmdTable
   
   ' 打印记录集中的当前数据，从作者表中添加作者姓名。
   Debug.Print "Authors with " &amp; intRoyalty &amp; _
      " percent royalty"
   Do While Not rstByRoyalty.EOF
      strAuthorID = rstByRoyalty!au_id
      Debug.Print , rstByRoyalty!au_id &amp; ", ";
      rstAuthors.Filter = "au_id = '" &amp; strAuthorID &amp; "'"
      Debug.Print rstAuthors!au_fname &amp; " " &amp; _
         rstAuthors!au_lname
      rstByRoyalty.MoveNext
   Loop

   rstByRoyalty.Close
   rstAuthors.Close
   cnn1.Close
   
End Sub</pre>
</BODY>
</HTML>
