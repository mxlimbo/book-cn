<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 3.2 Final//EN">
<HTML dir=ltr>
<HEAD>
<META HTTP-EQUIV="Content-Type" Content="text/html; charset=GB_2312-80">
<title>OpenSchema 方法范例</title>
<style>@import url(/stylesheets/msdn_ie4.css);</style>
<link disabled rel="stylesheet" href="/stylesheets/msdn_ie3.css"></HEAD>
<BODY>

<h3><a name="mdmthopenschemax"></a>OpenSchema 方法范例</h3>
<p>
该范例使用 <b>OpenSchema</b> 方法显示 Pubs 数据库内每个表的名称和类型。</p>
<pre>Public Sub OpenSchemaX()

   Dim cnn1 As ADODB.Connection
   Dim rstSchema As ADODB.Recordset
   Dim strCnn As String
      
   Set cnn1 = New ADODB.Connection
      strCnn = "Provider=sqloledb;" &amp; _
      "Data Source=srv;Initial Catalog=pubs;User Id=sa;Password=; "
   cnn1.Open strCnn
      
   Set rstSchema = cnn1.OpenSchema(adSchemaTables)
   
   Do Until rstSchema.EOF
      Debug.Print "Table name: " &amp; _
         rstSchema!TABLE_NAME &amp; vbCr &amp; _
         "Table type: " &amp; rstSchema!TABLE_TYPE &amp; vbCr
      rstSchema.MoveNext
   Loop
   rstSchema.Close
   
   cnn1.Close
   
End Sub
</pre>
<p>
该范例在 <b>OpenSchema</b> 方法的 <b>Criteria</b> 参数中指定 TABLE_TYPE 查询约束。因此，只返回在 Pubs 数据库中指定视图的模式信息。然后该范例显示每个表的名称和类型。</p>
<pre>Public Sub OpenSchemaX2()

   Dim cnn2 As ADODB.Connection
   Dim rstSchema As ADODB.Recordset
   Dim strCnn As String
      
   Set cnn2 = New ADODB.Connection
      strCnn = "Provider=sqloledb;" &amp; _
      "Data Source=srv;Initial Catalog=pubs;User Id=sa;Password=; "
   cnn2.Open strCnn
      
   Set rstSchema = cnn2.OpenSchema(adSchemaTables, Array(Empty, Empty, Empty, "VIEW"))

      Do Until rstSchema.EOF
         Debug.Print "Table name: " &amp; _
            rstSchema!TABLE_NAME &amp; vbCr &amp; _
            "Table type: " &amp; rstSchema!TABLE_TYPE &amp; vbCr
         rstSchema.MoveNext
      Loop
   rstSchema.Close
   
   cnn2.Close
   
End Sub
</pre>
</BODY>
</HTML>
